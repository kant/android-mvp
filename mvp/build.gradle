apply plugin: 'com.android.library'

group 'com.moovel.mvp'
version "$libraryVersion"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "$version"
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        disable "GradleDependency", "OldTargetApi"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

configurations {
    lintChecks
}

dependencies {

    compile "com.android.support:appcompat-v7:$supportLibraryVersion"

    testCompile libraries.junit
    testCompile libraries.mockito
    testCompile "org.robolectric:robolectric:3.7.1"
    testCompile "org.robolectric:shadows-support-v4:3.3.2"

    lintChecks project(path: ':mvp-lint', configuration: 'lintChecks')
}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

apply plugin: 'com.github.ben-manes.versions'
apply from: 'maven.gradle'
apply from: '../checkstyle.gradle'
