apply plugin: 'com.android.library'

group 'com.moovel.mvp'
version "$libraryVersion"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "$version"
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

configurations {
    lintChecks
}

dependencies {

    compile libraries.android.support.compat

    testCompile libraries.junit
    testCompile libraries.mockito

    lintChecks project(path: ':mvp-lint', configuration: 'lintChecks')
}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

apply plugin: 'com.github.ben-manes.versions'
apply from: 'maven.gradle'
apply from: '../checkstyle.gradle'
